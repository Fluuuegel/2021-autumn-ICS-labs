# ICS Lab2 实验报告

> 张艺耀 PB2011630

## 原初设计思路

初始化 将F(0) F(1) F(2)分别存在R1 R2 R3中。

当R3 = F$_{n}$时 计算F$_{n+1}$ = 2*R1 + R3 存到R1、R7；

当R1 = F$_{n}$时 计算F$_{n+1}$ = 2*R2 + R1 存到R2、R7；

当R2 = F$_{n}$时 计算F$_{n+1}$ = 2*R3 + R2存到R3、R7 ；以此类推。

R0用于计数，每次循环R0 = R0 -  1，当 R0 = 0时halt。

为了实现模1024的操作，我们将R5初始化为1，R6初始化为10用于计数。将R5左移10位后取反 R4 = R5左移1位取反 将R4和R5相与的结果存入R5中 得到11位12位均为0，其余位均为1的数，这样才能保证在每次计算过程中只需将相应寄存器内值与R5内值相与即可得到模1024的结果。

最后将相应的计算结果存到R7中即可。

考虑到 n = 1， 2的情况，我们需要加个简单的特判。

## 原初代码实现

```assembly
    .ORIG x3000
        ADD R1,R1,#1
        ADD R2,R2,#1
        ADD R3,R3,#2
        ADD R5,R5,#1
        ADD R6,R6,#10
AGAIN   ADD R5,R5,R5
        ADD R6,R6,#-1
        BRp AGAIN
        ADD R4,R5,R5
        NOT R4,R4
        NOT R5,R5
        AND R5,R4,R5
        ADD R0,R0,#-2;if R0 = 1, 2
        BRz TOED1
        BRn TOED2
LABEL   ADD R0,R0,#-1
        BRn TOED
        ADD R1,R1,R1
        ADD R1,R3,R1
        AND R1,R1,R5
        ADD R7,R1,#0
        ADD R0,R0,#-1
        BRn TOED
        ADD R2,R2,R2
        ADD R2,R1,R2
        AND R2,R2,R5
        ADD R7,R2,#0
        ADD R0,R0,#-1
        BRn TOED
        ADD R3,R3,R3
        ADD R3,R2,R3
        AND R3,R3,R5
        ADD R7,R3,#0
        BRnzp LABEL
TOED1		ADD R7,R7,#1
TOED2 	ADD R7,R7,#1
TOED    .BLKW   #1
        .END
N1  .FILL   x03A2
N2  .FILL   x00F6
N3  .FILL   x0182
N4  .FILL   x0332
```

简单c语言验证程序：

```c
#include<stdio.h>
int main(){
    int n, count = 1, out;
    while(1){
        scanf("%d", &n);
            if(!n) break;
        int x = 1, y = 1, z = 2;
        if(n == 1||n == 2) out = 1;
        for(int i = 2; i < n; i++,count++){
            if(count%3 == 1){
                x = (2*x + z)%1024;
                out = x;
            }
            else if(count%3 == 2){
                y = (2*y + x)%1024;
                out = y;
            }
            else{
                z = (2*z + y)%1024;
                out = z;
            }
        }
        printf("N = %d Result: %d\n",n, out);
        x = y = 1; z = 2; count = 1;
    }
    return 0;
}
```

将本人学号20 11 16 30代入得计算结果分别为：

`930 246 386 818`

对应16进制数分别为:

`x03A2 x00F6 x0182 x0332`

## 最终实现

可以看到这种方式实现的代码行数较多，因为R1 R2 R3轮换存F$_{n+1}$的值，很不实用。我们考虑只用R3存F$_{n+1}$的值。

我们考虑用R4暂存R2的值，顺序轮换。

flowchart如下：

```flow
i1=>inputoutput: R4 <- R2
i2=>inputoutput: R2 <- R3
i3=>inputoutput: R3	<- 2R1+R3
i4=>inputoutput: R1 <- R4
i1->i2->i3->i4
```

代码如下：

```assembly
    .ORIG x3000
        ADD R1,R1,#1
        ADD R2,R2,#1
        ADD R3,R3,#2
        ADD R5,R5,#1
        ADD R6,R6,#10
AGAIN   ADD R5,R5,R5
        ADD R6,R6,#-1
        BRp AGAIN
        ADD R4,R5,R5
        NOT R4,R4
        NOT R5,R5
        AND R5,R4,R5
        ADD R0,R0,#-2;if R0 = 0,1
        BRz TOED1
        BRn TOED2
LABEL   ADD R0,R0,#-1
        BRn TOED
        ADD R4,R2,#0
        ADD R2,R3,#0
        ADD R1,R1,R1
        ADD R3,R3,R1
        AND R3,R5,R3
        ADD R1,R4,#0
        ADD R7,R3,#0
        BRnzp LABEL
TOED1   ADD R7,R7,#1;if R0 = 2
TOED2   ADD R7,R7,#1;if R0 = 0,1
TOED    HALT
        .END
N1  .FILL   x03A2
N2  .FILL   x00F6
N3  .FILL   x0182
N4  .FILL   x0332
```

共34行 除去寄存器初始化14行。

经验证，程序正确

![截屏2021-12-11 上午10.57.10](/Users/fluegelcat/Desktop/Snapshot/截屏2021-12-11 上午10.57.10.png)

## 优化

进行了一些减少代码行数的优化（将x03FF事先存在内存中 这样就不用在运行时计算了

```assembly
    .ORIG x3000
        ADD R1,R1,#1
        ADD R2,R2,#1
        ADD R3,R3,#2
        LD  R5, CMP
        ADD R0,R0,#-2;if R0 = 0,1
        BRz TOED1
        BRn TOED2
LABEL   ADD R0,R0,#-1
        BRn TOED
        ADD R4,R2,#0
        ADD R2,R3,#0
        ADD R1,R1,R1
        ADD R3,R3,R1
        AND R3,R5,R3
        ADD R1,R4,#0
        ADD R7,R3,#0
        BRnzp LABEL
TOED1   ADD R7,R7,#1;if R0 = 2
TOED2   ADD R7,R7,#1;if R0 = 0,1
TOED    HALT
N1  .FILL   x03A2
N2  .FILL   x00F6
N3  .FILL   x0182
N4  .FILL   x0332
CMP	.FILL 	x03FF
		.END
```

